// Pest grammar for the gnoll-roll discord bot
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }  // Really just " " should be sufficient but people are weird.

// Support math
operation = _{ add | subtract | multiply | divide }
    add      = { "+" }
    subtract = { "-" }
    multiply = { "*" }
    divide   = { "/" }
comparison = _{ lte | gte | lt | gt | eq }  // important to maintain this order
    lte     = { "<=" }
    gte     = { ">=" }
    lt      = { "<" }
    gt      = { ">" }
    eq      = { "==" }

counting_number = @{ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }
natural_number = @{ "0" | counting_number }
number = @{"-" ? ~ natural_number }

// Suffixes for roll modifiers
// TODO

// Rules for rolling
failure_rule = { "f" ~ counting_number }
success_count = { comparison ~ natural_number ~ failure_rule* }
keep = { ("kl" | "k") ~ natural_number }
reroll_rule = { "r" ~ natural_number }
die_roll = { natural_number ~ "d" ~ natural_number ~ ( keep | failure_rule | reroll_rule )? }
item = _{  die_roll | number }

comment_text = { ANY* }
comment = { "#" ~ comment_text }
roll = { SOI ~ (item ~ (operation ~ item)* ~ comment?) ~ EOI }
